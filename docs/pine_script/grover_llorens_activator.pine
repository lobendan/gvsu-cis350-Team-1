// This work is licensed under a Creative Commons Attribution-ShareAlike 4.0 International License https://creativecommons.org/licenses/by-sa/4.0/
// © alexgrover & Lucía Llorens

//@version=4
study("Grover Llorens Activator [alexgrover & Lucía Llorens]","GLA",overlay=true)
length = input(500),mult = input(2),src = input(close)
//----
ts = 0.,os = 0
diff = src - nz(ts[1],src[1])
atr = atr(length)
os := rising(src,length) ? 1 : falling(src,length) ? -1 : os[1]
//----
rise = change(os) == 2 and diff[1] < 0
fall = change(os) == -2 and diff[1] > 0
up = crossover(diff,0) 
dn = crossunder(diff,0) 
//----
val = valuewhen(up or dn or rise or fall,atr/length,0)
bars = barssince(up or dn or rise or fall)
ts := up ? nz(ts[1],src) - atr*mult : 
  dn ? nz(ts[1],src) + atr*mult : 
  rise ? src - atr*mult :
  fall ? src + atr*mult :
  nz(ts[1],src) + sign(diff)*val*bars
//----
css = diff > 0 ? #2196f3 : #ff1100
plotshape(iff(crossover(diff,0),ts,na),"Buy Circle",shape.circle,location.absolute,#2196f3,0,size=size.tiny)
plotshape(iff(crossunder(diff,0),ts,na),"Sell Circle",shape.circle,location.absolute,#ff1100,0,size=size.tiny)

plotshape(iff(crossover(diff,0),ts,na),"Buy Label",shape.labelup,location.absolute,#2196f3,0,text="Buy",textcolor=color.white,size=size.tiny)
plotshape(iff(crossunder(diff,0),ts,na),"Sell Label",shape.labeldown,location.absolute,#ff1100,0,text="Sell",textcolor=color.white,size=size.tiny)